rule MacbookRule "When you buy Macbook, you got Raspberry PI B For free" {
    when
        CartItem.SKU == "43N23P" && MappedCartItem["234234"].Qty > 0
    then
        Log(MappedSku["234234"].String());
        MappedSku["234234"].Price = 0;
        Retract("MacbookRule");
}

rule GoogleSpeaker "When you buy 3 google speaker, only pay for 2" {
    when
        CartItem.SKU == "120P90" && MappedCartItem[CartItem.SKU].Qty >= 3
    then
        MappedCartItem[CartItem.SKU].Qty = MappedCartItem[CartItem.SKU].Qty - 1;
        Retract("GoogleSpeaker");
}

rule AlexSpeaker "When you buy more than 3 alexa speakers, got 10% off on each item of this speakers" {
    when
        CartItem.SKU == "A304SD" && MappedCartItem[CartItem.SKU].Qty >= 3
    then
        MappedSku[CartItem.SKU].Price = MappedSku[CartItem.SKU].Price - (MappedSku[CartItem.SKU].Price * 10 / 100);
        Retract("AlexSpeaker");
}


rule GeneralPrice "Otherwise" {
    when
        MappedCartItem[CartItem.SKU].Qty > 0
    then
        Log(MappedSku[CartItem.SKU].String());
        Checkout.TotalPrice = Checkout.TotalPrice + MappedSku[CartItem.SKU].Price * CartItem.Qty;
        MappedCartItem[CartItem.SKU].Qty = 0;
}
